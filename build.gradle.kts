plugins {
    id("java")
    id("org.jetbrains.kotlin.jvm") version "2.3.20"
    id("org.jetbrains.intellij.platform") version "2.16.0"
}

group = "de.ljunker"
version = providers.gradleProperty("pluginVersion").get()

val kasmVersion = providers.gradleProperty("kasmVersion")
val kasmProjectDir = providers.gradleProperty("kasmProjectDir").orElse("../Kasm")
val defaultKasmJarPath = kasmProjectDir.zip(kasmVersion) { projectDir, version ->
    "$projectDir/build/libs/Kasm-$version.jar"
}
val kasmJarPath = providers.gradleProperty("kasmJarPath").orElse(defaultKasmJarPath)

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

// Read more: https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin.html
dependencies {
    implementation(files(kasmJarPath.get()))
    testImplementation("junit:junit:4.13.2")

    intellijPlatform {
        intellijIdea("2025.2.4")
        testFramework(org.jetbrains.intellij.platform.gradle.TestFrameworkType.Platform)


        // Add plugin dependencies for compilation here, example:
        // bundledPlugin("com.intellij.java")
    }
}

intellijPlatform {
    pluginConfiguration {
        ideaVersion {
            sinceBuild = "252.25557"
        }

        changeNotes = """
            New Version of KASM
        """.trimIndent()
    }

    signing {
        certificateChain = providers.gradleProperty("certificateChain")
        privateKey = providers.gradleProperty("privateKey")
        password = providers.gradleProperty("privateKeyPassword")
    }

    publishing {
        token = providers.gradleProperty("intellijPlatformPublishingToken")
    }
}

tasks {
    // Set the JVM compatibility versions
    withType<JavaCompile> {
        sourceCompatibility = "21"
        targetCompatibility = "21"
    }
}

kotlin {
    compilerOptions {
        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21)
    }
}
